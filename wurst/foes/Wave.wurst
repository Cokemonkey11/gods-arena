package Wave
import public ClosureTimers
import public LinkedList
import PlayerData
import Creep
import CreepObjEditing
import ObjectIdGenerator
import SoundUtils
import Abilities
import Funcs
import GodOfNature
import GodOfNatureStrong

constant AFTER_WAVE_RESTORATION_SOUND = new SoundDefinition(Sounds.restorationPotion)

interface NextStepListener
  abstract function execute()

abstract class WaveStep
  Wave wave

  construct (Wave wave)
    this.wave = wave

  abstract function execute(NextStepListener next)

class WaitStep extends WaveStep
  var duration = 0.
 

  construct(Wave wave, real duration)
    super(wave)
    this.duration = duration

  override function execute(NextStepListener next)
    doAfter(duration) -> 
      next.execute()


class CreepStep extends WaveStep
  let unitId = UNIT_ID_GEN.next()
  CreepDefinition creepDef

  var amount = 1
  var bounty = 0

  construct(Wave wave, int origUnitId)
    super(wave)
    creepDef = new CreepDefinition(unitId, origUnitId)

  override function execute(NextStepListener next)
    doPeriodicallyCounted(0.05, amount) cb ->
      new Creep(unitId, wave.spawnPoint.execute().toVec3(), GetRandomReal(0, 360).fromDeg(), bounty)
      if cb.isLast()
        next.execute()

class BossStep extends WaveStep
  static var bossNumber = 0

  construct(Wave wave)
    super(wave)

  override function execute(NextStepListener next)
    bossNumber++
    if bossNumber == 1
      new GodOfNature(wave.spawnPoint.execute().toVec3(), GetRandomReal(0, 360).fromDeg())
    else if bossNumber == 2
      new GodOfNatureStrong(wave.spawnPoint.execute().toVec3(), GetRandomReal(0, 360).fromDeg())
    next.execute()

public class WaveBuilder
  let result = new Wave()
  let steps = new LinkedList<WaveStep>
  var currentUnitId = '0000'
  WaveStep currentStep = null
  CreepStep creepStep = null

  function wait(real duration)
    if currentStep != null
      steps.add(currentStep)
      currentStep = null

      steps.add(new WaitStep(result, duration))

  function creep(int origUnitId)
    if currentStep != null
      steps.add(currentStep)
      currentStep = null

    creepStep = new CreepStep(result, origUnitId)
    currentStep = creepStep

  function boss()
    currentStep = new BossStep(result)

  function abilities(string abilities)
    creepStep.creepDef.setNormalAbilities(abilities)

  function amount(int amount)
    creepStep.amount = amount

  function hp(int hp)
    creepStep.creepDef.setHitPointsMaximumBase(hp)
    creepStep.bounty = max(1, (hp / 375).round())
    if creepStep.bounty > 10
      creepStep.bounty = (creepStep.bounty * .70).round()
    else if creepStep.bounty > 25
      creepStep.bounty = (creepStep.bounty * .45).round()

  function mana(int mana)
    creepStep.creepDef.setManaMaximum(mana)
    creepStep.creepDef.setManaInitialAmount(mana)

  function damage(int dmg)
    creepStep.creepDef.setAttack1DamageBase(dmg)

  function attackrate(real rate)
    creepStep.creepDef.setAttack1CooldownTime(rate)

  function armor(int armor)
    creepStep.creepDef.setDefenseBase(armor)

  function armorType(ArmorType armorType)
    creepStep.creepDef.setArmorType(armorType)

  function model(string path)
    creepStep.creepDef.setModelFile(path)

  function scale(real scale)
    creepStep.creepDef.setScalingValue(scale)

  function rgb(int red, int green, int blue)
    creepStep.creepDef.setTintingColorRed(red)
    creepStep.creepDef.setTintingColorGreen(green)
    creepStep.creepDef.setTintingColorBlue(blue)

  function goldBounty(int bounty)
    // this.result.goldBounty = bounty
    
  function expBounty(int bounty)
    this.result.expBounty = bounty

  function build() returns Wave
    if currentStep != null
      steps.add(currentStep)
      currentStep = null
    result.steps = steps
    return result

constant NEXT_WAVE = new SoundDefinition(Sounds.questNew)
constant WAVE_FINISHED = new SoundDefinition(Sounds.questCompleted)

public class Wave
  LinkedList<WaveStep> steps
  Vec2Function spawnPoint
  Vec2Function revivePoint
  
  var goldBounty = 50
  var expBounty = 100

  var isDone = false

  function spawn()
    printTimed("|cffceac15>> The wave begins!", 10.)
    NEXT_WAVE.play()
    nextStep()
      
  function nextStep()
    if steps.isEmpty()
      isDone = true
    else
      steps.dequeue().execute() -> 
        nextStep()
      

  function finish()
    printTimed("|cff1f9913>> Wave cleared!", 10.)
    WAVE_FINISHED.play()
    //+ TODO - temporary decision - One active player gets 50% additional gold and exp.
    //  And for each additional player additional bounty decreased by 1/7.
    //  So at 8 players there won't be any additional bounty, just wave based values.
    let additionalExpBounty = (expBounty * 0.5 * (8-pData.size())/7).round()
    let additionalGoldBounty = (goldBounty * 0.5 * (8-pData.size())/7).round()
    //-

    pData.forEach() (player p, PlayerData playerData) -> 
      let heroUnit = playerData.getHero().actor
      p.addGold(goldBounty + additionalGoldBounty)
      playerData.getHero().addXp(expBounty + additionalExpBounty, true)
    
      if not heroUnit.isAlive()
        heroUnit.revive(revivePoint.execute(), true)
        heroUnit.setHP(heroUnit.getMaxHP() * 0.5)
        heroUnit.setMana(heroUnit.getMaxMana() * 0.5)
        p.select(heroUnit)
        p.setCameraPosition(heroUnit.getPos())
      else 
        AFTER_WAVE_RESTORATION_SOUND.play(1, 30)
        flashEffect(Abilities.replenishHealthCasterOverhead, heroUnit.getPos())
        heroUnit.setHP(heroUnit.getMaxHP())
        heroUnit.setMana(heroUnit.getMaxMana())
    