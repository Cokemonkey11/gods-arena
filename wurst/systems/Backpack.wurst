package Backpack
import ObjectIdGenerator
import Players
import ClosureEvents
import ClosureTimers
import ItemShopUI
import Icons
import UnitIds
import AbilityIds
import HeroPreset
import PlayerData
import HashMap

constant BACKPACK_ID = compiletime(HERO_ID_GEN.next())

public constant backpacks = new IterableMap<unit, Backpack>

init
  EventListener.add(EVENT_PLAYER_UNIT_SELECTED) -> 
    let selectedUnit = EventData.getTriggerUnit()

    if selectedUnit.hasAbility(AbilityIds.inventory)
      let shopWasVisible = shop.itemShopFrame.isVisible()
      shop.showToUnit(selectedUnit)
      // showToUnit() opens shop for now, so need to return shop in its initial state before unit selection
      if not shopWasVisible
        shop.toggleView(selectedUnit.getOwner(), selectedUnit)

  doPeriodically(1) (CallbackPeriodic cb) ->
    updateBackpackPos()

  onPlayerLeave() p ->
    for backpack in backpacks
      if backpack.getOwner() == p
        backpacks.remove(backpack)
        break

function updateBackpackPos()
  for backpack in backpacks
    pData.get(backpack.getOwner()).getHero().ifPresent() (h) ->
      backpack.setPos(h.actor.getPos())

public class Backpack
  constant unit backpackUnit
  constant unit backpackHero

  construct(player p, unit hero)
    backpackUnit = createUnit(p, BACKPACK_ID, vec2(0, 0), angle(0))
    backpacks.put(backpackUnit, this)
    backpackHero = hero

@compiletime function genBackpackDefinition()
  new HeroPreset(BACKPACK_ID, UnitIds.circleofpower, "Backpack")
  ..addProperName("Backpack")
  ..setIconGameInterface(Icons.bTNPackBeast)
  ..setLevel(50)
  ..addNormalAbility(AbilityIds.invulnerable)
  ..setAttacksEnabled(0)
  ..setArmorType(ArmorType.Divine)
  ..setModelFile("")
  ..setScalingValue(0.)
  ..setSelectionScale(0)
  ..setCollisionSize(0.)
  ..setSightRadiusDay(0)
  ..setSightRadiusNight(0)
  ..buildHero()