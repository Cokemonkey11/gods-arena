package OrbOfMagic
import ItemObjEditing
import RecipeItem
import ChannelAbilityPreset
import Icons
import ClosureEvents
import SoundUtils
import Sapphire
import TooltipFactory

constant ORB_OF_MAGIC_ID = compiletime(ABIL_ID_GEN.next())

constant ABIL_TOOLTIP = itemAbilityTooltip(AbilityType.ACTIVE,
  "Refresh", 
  "refreshes all abilities of the owner.")

public constant ORB_OF_MAGIC_ITEM = compiletime(new RecipeItem()
  ..setName("Orb of Magic")
  ..setInterfaceIcon(Icons.bTNCrystalBall)
  ..setGoldCost(1000)
  ..setActivelyUsed(true)
  ..addBonus(Bonus.INTELLIGENCE, 50)
  ..setAbilities(ORB_OF_MAGIC_ID.toRawCode())
  ..setCooldownGroup(ORB_OF_MAGIC_ID.toRawCode())
  ..generateTooltip(ABIL_TOOLTIP)
)..register()

public constant ORB_OF_MAGIC_RECIPE = new ItemRecipe()
..gold(100)
..setRequiredItems(SAPPHIRE_ITEM.getNewId(), SAPPHIRE_ITEM.getNewId(), SAPPHIRE_ITEM.getNewId(), SAPPHIRE_ITEM.getNewId(), SAPPHIRE_ITEM.getNewId(), SAPPHIRE_ITEM.getNewId())
..setResultItem(ORB_OF_MAGIC_ITEM)

init
  EventListener.onCast(ORB_OF_MAGIC_ID) (unit caster) ->
    new SoundDefinition(Sounds.wandOfIllusionTarget1).playForPlayer(caster.getOwner())
    caster.resetCooldown()

@compiletime function genItemAbils()
  new ChannelAbilityPreset(ORB_OF_MAGIC_ID, 1, true)
  ..setManaCost(1, 150)
  ..setCooldown(1, 100)
