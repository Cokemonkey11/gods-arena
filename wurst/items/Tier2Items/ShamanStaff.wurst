package ShamanStaff
import Icons
import UnitIds
import RecipeItem
import AbilityObjEditing
import ObjectIdGenerator
import ObjectIds
import ApprenticeStaff
import ShamanClaws

constant HEALING_WARD_ID = compiletime(ABIL_ID_GEN.next())
constant HEALING_WARD_UNIT_ID = compiletime(UNIT_ID_GEN.next())
constant HEALING_WARD_AURA_ID = compiletime(UNIT_ID_GEN.next())

public constant SHAMAN_STAFF_ITEM = compiletime(new RecipeItem()
..setName("Shaman Staff")
..setInterfaceIcon(Icons.bTNWitchDoctorAdept)
..setActivelyUsed(true)
..addBonus(Bonus.DAMAGE, 30)
..addBonus(Bonus.ATTACKSPEED, 20)
..addBonus(Bonus.INTELLIGENCE, 15)
..addBonus(Bonus.MANAREGEN, 1.5)
..setAbilities(commaList(HEALING_WARD_ID))
..setCooldownGroup(HEALING_WARD_ID.toRawCode())
..generateTooltip("Active - Healing Ward - places healing ward, which heals 1% hp per second. Lasts 15 seconds.")
)..register()

public constant SHAMAN_STAFF_RECIPE = new ItemRecipe()
..gold(220)
..setRequiredItems(APPRENTICE_STAFF_ITEM.getNewId(), SHAMAN_CLAWS_ITEM.getNewId())
..setResultItem(SHAMAN_STAFF_ITEM)

@compiletime function genItemAbils()
  new AbilityDefinitionHealingWard(HEALING_WARD_ID)
  ..setWardUnitType(1, HEALING_WARD_UNIT_ID.toRawCode())
  ..setDurationNormal(1, 15)
  ..setManaCost(1, 125)
  ..setCooldown(1, 60)

  new UnitDefinition(HEALING_WARD_UNIT_ID, UnitIds.healingward)
  ..setNormalAbilities(HEALING_WARD_AURA_ID.toRawCode())

  new AbilityDefinitionAuraRegenerationHealingWard(HEALING_WARD_AURA_ID)
  ..setAmountofHitPointsRegenerated(1, 0.01)