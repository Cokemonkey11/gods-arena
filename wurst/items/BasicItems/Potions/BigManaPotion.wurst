package BigManaPotion
import ItemObjEditing
import ObjectIdGenerator
import AbilityObjEditing
import RecipeItem
import ObjectIds
import ClosureEvents
import UnitExtFuncs
import SmallManaPotion

constant MANA_ID = compiletime(ABIL_ID_GEN.next())
constant FLAT_MP = 525
constant PERC_MP = 30

public constant B_MANA_POT_ITEM = compiletime(new RecipeItem()
  ..setName("Big Mana Potion")
  ..setInterfaceIcon("ReplaceableTextures/CommandButtons/BTNBigManaPotion.blp")
  ..setGoldCost(115)
  ..setNumberofCharges(1)
  ..setMaxStack(5)
  ..setActivelyUsed(true)
  ..setPerishable(true)
  ..setAbilities(MANA_ID.toRawCode())
  ..setCooldownGroup(MANA_COOLDOWN_GROUP_ID.toRawCode())
  ..generateTooltip("Restores "+ FLAT_MP.toString() +" or "+ PERC_MP.toString() +"% mp, which value is bigger.")
)..register()

init 
  EventListener.onCast(MANA_ID) (unit caster) ->
    let percMP = caster.getMaxMana() * PERC_MP/100
    
    if FLAT_MP > percMP
      caster.restoreMP(FLAT_MP)
    else
      caster.restoreMP(percMP)

@compiletime function genItemAbils()
  new AbilityDefinitionItemManaRestoreLesser(MANA_ID)
  ..presetManaPointsGained(lvl -> 0)
  ..presetCooldown(lvl -> 20)

