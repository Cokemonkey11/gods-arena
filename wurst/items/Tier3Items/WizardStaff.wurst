package WizardStaff
import Icons
import RecipeItem
import AbilityObjEditing
import ObjectIdGenerator
import ObjectIds
import AdeptStaff
import TooltipFactory

constant FIREBOLT_ID = compiletime(ABIL_ID_GEN.next())
constant FIREBOLT_DAMAGE = 500
constant FIREBOLT_STUN = 0.75

constant ABIL_TOOLTIP = itemAbilityTooltip(AbilityType.ACTIVE,
  "Fire Bolt", 
  "fires firebolt which deals " + FIREBOLT_DAMAGE.color(COLOR_GOLD)
  + " damage and stuns target for " + FIREBOLT_STUN.color(COLOR_GOLD) + " seconds.")

public constant WIZARD_STAFF_ITEM = compiletime(new RecipeItem()
..setName("Wizard Staff")
..setInterfaceIcon(Icons.bTNWandOfNeutralization)
..setActivelyUsed(true)
..addBonus(Bonus.INTELLIGENCE, 35)
..addBonus(Bonus.MANAREGEN, 3.5)
..setAbilities(commaList(FIREBOLT_ID))
..setCooldownGroup(FIREBOLT_ID.toRawCode())
..generateTooltip(ABIL_TOOLTIP)
)..register()

public constant WIZARD_STAFF_RECIPE = new ItemRecipe()
..gold(290)
..setRequiredItems(ADEPT_STAFF_ITEM.getNewId())
..setResultItem(WIZARD_STAFF_ITEM)

@compiletime function genItemAbils()
  new AbilityDefinitionFireBoltcreep(FIREBOLT_ID)
  ..setDamage(1, FIREBOLT_DAMAGE.toReal())
  ..setDurationNormal(1, FIREBOLT_STUN)
  ..setDurationHero(1, FIREBOLT_STUN)
  ..setManaCost(1, 125)
  ..setCooldown(1, 16)