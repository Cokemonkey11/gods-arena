package ItemShopMenu
import FramehandleNames
import ClosureFrames
import HashMap
import HashSet
import Hero
import PlayerData
import SoundUtils
import Items
import ClosureTimers
import Players

public ItemShop itemShop

constant playerVisibleFrames = new HashMap<player, HashSet<framehandle>>

constant itemShopOpenSound = new SoundDefinition(Sounds.bigButtonClick)

public function initItemShopMenu()
  itemShop = new ItemShop()
  addItemIcons()
  doPeriodically(0.5) cb ->
    ALL_PLAYERS.forEach() p ->
      itemShop.checkBuyability(p)


function framehandle.removeFocus()
  this.disable()
  this.enable()

function addItemIcons()
  itemShop
  ..addItemIcon(new ItemIcon(HEALING_POTION_ID, 50, true))
  ..addItemIcon(new ItemIcon(BIG_HEALING_POTION_ID, 125, true))
  ..addItemIcon(new ItemIcon(MANA_POTION_ID, 40, true))
  ..addItemIcon(new ItemIcon(BIG_MANA_POTION_ID, 90, true))
  ..addItemIcon(new ItemIcon(VAMPIRIC_POTION_ID, 500, false))
  ..addItemIcon(new ItemIcon(HEALING_WARD_ID, 500, false))
  ..addItemIcon(new ItemIcon(SCROLL_OF_HEALING_ID, 180, false))
  ..addItemIcon(new ItemIcon(SCROLL_OF_MANA_ID, 130, false))
  ..addItemIcon(new ItemIcon(SCROLL_OF_RESTORATION_ID, 600, false))
  ..addItemIcon(new ItemIcon(SCROLL_OF_PROTECTION_ID, 300, false))
  ..addItemIcon(new ItemIcon(SCROLL_OF_BEAST_ID, 500, false))
  ..addItemIcon(new ItemIcon(TOME_OF_STRENGTH_ID, 250, false))
  ..addItemIcon(new ItemIcon(TOME_OF_AGILITY_ID, 250, false))
  ..addItemIcon(new ItemIcon(TOME_OF_INTELLIGENCE_ID, 250, false))
  ..addItemIcon(new ItemIcon(GAUNTLETS_OF_OGRE_STRENGTH_ID, 115, false))
  ..addItemIcon(new ItemIcon(SLIPPERS_OF_AGILITY_ID, 100, false))
  ..addItemIcon(new ItemIcon(MANTLE_OF_INTELLIGENCE_ID, 90, false))
  ..addItemIcon(new ItemIcon(CIRCLET_OF_NOBILITY_ID, 155, false))
  ..addItemIcon(new ItemIcon(GLOVES_OF_HASTE_ID, 180, false))
  ..addItemIcon(new ItemIcon(RING_OF_PROTECTION_ID, 200, false))
  ..addItemIcon(new ItemIcon(RING_OF_REGENERATION_ID, 180, false))
  ..addItemIcon(new ItemIcon(SOBI_MASK_ID, 170, false))
  ..addItemIcon(new ItemIcon(PENDANT_OF_MANA_ID, 250, false))
  ..addItemIcon(new ItemIcon(WARSONG_BATTLE_DRUMS_ID, 600, false))
  ..addItemIcon(new ItemIcon(LEGION_DOOM_HORN_ID, 600, false))
  ..addItemIcon(new ItemIcon(KHADGARS_PIPE_OF_INSIGHT_ID, 600, false))

function addShopHotkeyForPlayer(player p)
  let trig = CreateTrigger()
  ..addAction() ->
    let p2 = GetTriggerPlayer()
    let currentPlayerVisibleFrames = playerVisibleFrames.get(p2)
    
    itemShopOpenSound.playForPlayer(p2)

    if currentPlayerVisibleFrames.has(itemShop.itemShopFrame)
      itemShop.itemShopFrame.hide(p2)
      currentPlayerVisibleFrames.remove(itemShop.itemShopFrame)
    else
      itemShop.itemShopFrame.show(p2)
      currentPlayerVisibleFrames.add(itemShop.itemShopFrame) 

  BlzTriggerRegisterPlayerKeyEvent(trig, p, OSKEY_G, 0, false)

class ItemShop
  private var items = new LinkedList<ItemIcon>

  private constant PADDING = 0.02
  private constant HORIZONTAL_SPACING = 0.03
  private constant VERTICAL_SPACING = -0.04
  private constant ITEM_ICON_INITIAL_POSITION = vec2(PADDING, -PADDING)
  private constant MAX_ICONS_PER_ROW = 7
  
  private var iconPosition = ITEM_ICON_INITIAL_POSITION

  constant itemShopFrame = createFrame("FRAME", "itemShopFrame", GAME_UI, "", 0)
  private constant itemShopBackdrop = createFrame("HeroPickMenuBackdrop", itemShopFrame, 0, 1) // Context is 1 cuz same name uses hero pick menu.
  private constant itemShopButton = createFrame("GeneralButton", GAME_UI, 0, 0)

  construct()
    itemShopFrame
    ..setSize(0.25, 0.315)
    ..setAbsPoint(FRAMEPOINT_TOP, vec2(0.68, 0.52))

    itemShopBackdrop.setAllPoints(itemShopFrame)

    itemShopButton
    ..setSize(0.09, 0.032)
    ..setAbsPoint(FRAMEPOINT_BOTTOM, vec2(0.55, 0.138))
    ..setText("Shop (G)")
    ..setScale(0.9)
    ..onClick() ->
      let p = GetTriggerPlayer()
      let currentPlayerVisibleFrames = playerVisibleFrames.get(p)

      this.itemShopButton.removeFocus()

      if currentPlayerVisibleFrames.has(itemShopFrame)
        itemShopFrame.hide(p)
        currentPlayerVisibleFrames.remove(itemShopFrame)
      else
        itemShopFrame.show(p)
        currentPlayerVisibleFrames.add(itemShopFrame) 
    
    // Hiding shop screen and button, since shop screen will be accesible through button,
    // and button will be shown after hero was selected. 
    pData.forEach() (player p, PlayerData playerData) ->
      itemShopFrame.hide(p)
      itemShopButton.hide(p)
      playerVisibleFrames.put(p, new HashSet<framehandle>)

  function addItemIcon(ItemIcon itemIcon)
    items.add(itemIcon)
    if items.size() > 1
      if (items.size() - 1) % MAX_ICONS_PER_ROW == 0
        iconPosition = iconPosition.add(-HORIZONTAL_SPACING * (MAX_ICONS_PER_ROW - 1), VERTICAL_SPACING)
      else
        iconPosition = iconPosition.add(HORIZONTAL_SPACING, 0)

    itemIcon.setIconPosition(iconPosition)

  function checkBuyability(player p)
    let gold = p.getGold()
    items.forEach(icon -> icon.checkBuyability(p, gold))

  function showShopButtonForPlayer(player p)
    itemShopButton.show(p)
    addShopHotkeyForPlayer(p)

class ItemIcon
  static private var globalIconContext = 0

  // Sounds
  static constant notEnoughGoldSound = new SoundDefinition(Sounds.necromancerNoGold1)
  static constant itemBoughtSound = new SoundDefinition(Sounds.pickUpItem)
  static constant cantBuyItemSound = new SoundDefinition(Sounds.error)

  private var itemId = 0
  private var itemCost = 0
  private var iconContext = 0
  private var iconPath = ""
  private var iconPathDisabled = ""
  private var stackable = false

  private framehandle itemIconButton = null
  private framehandle itemIcon = null
  private framehandle itemText = null

  construct(int itemId, int itemCost, boolean stackable)
    this.itemId = itemId
    this.itemCost = itemCost
    this.iconContext = globalIconContext
    this.stackable = stackable
    globalIconContext++

    itemIconButton = createFrame(FramehandleTypeNames.gluebutton, "ItemIconButton", itemShop.itemShopFrame, FramehandleNames.iconButtonTemplate, iconContext)
    itemIcon = createFrame(FramehandleTypeNames.backdrop, "itemIcon", itemIconButton, "", iconContext)
    itemText = createFrame(FramehandleTypeNames.text, "itemText", itemIconButton, "", iconContext)

    itemIconButton
      ..setSize(0.025, 0.025)
      ..onClick() -> 
      let p = GetTriggerPlayer()
      let hero = p.getData().getHero().actor

      itemIconButton.removeFocus()
      
      if hero.isAlive()
        if p.getGold() >= itemCost
          p.setGold(p.getGold() - itemCost)
          let itm = hero.getItemById(itemId)
          if stackable and itm != null
            hero.getItemById(itemId).setCharges(itm.getCharges() + 1)
          else
            hero.addItemById(itemId)
          itemBoughtSound.playForPlayer(p)
        else 
          notEnoughGoldSound.playForPlayer(p)
      else
        cantBuyItemSound.playForPlayer(p)

    let tempItem = createItem(itemId, vec2(0, 0))
    iconPath = tempItem.getIconPath()
    iconPathDisabled = iconPath.replace("BTN", "DISBTN").replace("CommandButtons", "CommandButtonsDisabled")
    tempItem.remove()    

    itemIcon
      ..setAllPoints(itemIconButton)
      ..setTexture(iconPath, 0, true)

    itemText
      ..setText(itemCost.toString())
      ..setTextColor(colorA(255, 215, 0, 1))
      ..setPoint(FRAMEPOINT_TOPLEFT, itemIconButton, FRAMEPOINT_BOTTOMLEFT, vec2(0, -0.001))

    createTooltip()

  function checkBuyability(player p, int gold)
    if p == localPlayer
      if this.itemCost <= gold
        itemIcon.setTexture(iconPath, 0, true)
        itemText.setTextColor(colorA(255, 215, 0, 1))
      else
        itemIcon.setTexture(iconPathDisabled, 0, true)
        itemText.setTextColor(colorA(200, 155, 0, 1))

  private function createTooltip()
    let tooltip = createFrame("BoxedText", itemIconButton, 0, iconContext)
    itemIconButton.setTooltip(tooltip)

    tooltip..setSize(0.28, 0.08)
      ..setPoint(FRAMEPOINT_RIGHT, itemIconButton, FRAMEPOINT_LEFT)

    let tempItem = createItem(itemId, vec2(0, 0))
    
    getFrame("BoxedTextTitle", iconContext).setText(tempItem.getTooltip())
    getFrame("BoxedTextValue", iconContext).setText(tempItem.getExtendedTooltip())

    tempItem.remove()

  function setIconPosition(vec2 iconPosition)
    itemIconButton.setPoint(FRAMEPOINT_TOPLEFT, itemShop.itemShopFrame, FRAMEPOINT_TOPLEFT, iconPosition)
