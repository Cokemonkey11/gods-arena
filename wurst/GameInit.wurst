package GameInit
import GameProperties
import EntityManagement
import WaveController
import PlayerData
import ItemShopMenu
import HeroPickMenu

constant arena1Rect = Rect(-2272, -1760, 2560, 1760)

function initFrames()
  let loadingResult = BlzLoadTOCFile("MyFiles.toc")
  if loadingResult == false
    print("SOMETHING WENT WRONG WITH MyFiles.toc")

function initPlayersGold()
  pData.forEach() (player p, PlayerData playerData) ->
    p.setGold(150)

function initVision()
  SetPlayerHandicap(ENEMY_PLAYER, 0.15 + 0.85 * (pData.size() / 8.))
  createVision(ENEMY_PLAYER, playableMapRect, true).start()

  pData.forEach() (player p, PlayerData playerData) ->
    createVision(p, arena1Rect, true).start()

function initPickTimer()
  let heroPickTimer = getTimer()
  let heroPickTimerDialog = heroPickTimer.createTimerDialog()
  heroPickTimerDialog
    ..setTitle("Hero pick phase ends in:")
    ..display(true)

  heroPickTimer.doAfter(HERO_PICK_TIME) ->
    heroPickTimerDialog.destr()
    waveController.startWaveSpawn()
        
init
  nullTimer() -> 
    initFrames()
    startEntityLoop()
    initPlayers()
    initPlayersGold()
    initVision()
    initPickTimer()
    initHeroPickMenu()
    initItemShopMenu()