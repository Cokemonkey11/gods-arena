package GameInit
import GameProperties
import EntityManagement
import PlayerData
import ItemShopUI
import HeroPickMenu
import Arenas
import ClosureTimers
import DamageEvent

constant arena1Rect = Rect(-2272, -1760, 2560, 1760)

function initFrames()
  let loadingResult = BlzLoadTOCFile("MyFiles.toc")
  if loadingResult == false
    print("SOMETHING WENT WRONG WITH MyFiles.toc")

function initPlayersGold()
  pData.forEach() (player p, PlayerData playerData) ->
    p.setGold(185)

function initVision()
  SetPlayerHandicap(ENEMY_PLAYER, 0.15 + 0.85 * (pData.size() / 8.))
  createVision(ENEMY_PLAYER, playableMapRect, true).start()

function initPickTimer()
  let heroPickTimer = getTimer()
  let heroPickTimerDialog = heroPickTimer.createTimerDialog()
  heroPickTimerDialog
    ..setTitle("Hero pick phase ends in:")
    ..display(true)

  heroPickTimer.doAfter(HERO_PICK_TIME) ->
    heroPickTimerDialog.destr()
    startArenas()

function initCheatCodes()
  pData.forEach() (player p, PlayerData playerData) ->
    if p.getName() == "Overkane" or p.getName() == "Frotty" or p.getName() == "Local Player"
      CreateTrigger()
      ..registerPlayerChatEvent(p, "!level", true)
      ..addAction() ->
        pData.get(GetTriggerPlayer()).getHero().ifPresent(h -> h.actor.setLevel(50, false))
      CreateTrigger()
      ..registerPlayerChatEvent(p, "!refresh", true)
      ..addAction() ->
        pData.get(GetTriggerPlayer()).getHero().ifPresent(h -> h.actor.setMana(h.actor.getMaxMana()))
        pData.get(GetTriggerPlayer()).getHero().ifPresent(h -> h.actor.setHP(h.actor.getMaxHP()))
        pData.get(GetTriggerPlayer()).getHero().ifPresent(h -> h.actor.resetCooldown())
      CreateTrigger()
      ..registerPlayerChatEvent(p, "!gold", true)
      ..addAction() ->
        GetTriggerPlayer().setGold(99999)
      CreateTrigger()
      ..registerPlayerChatEvent(p, "!debug", true)
      ..addAction() ->
        DamageEvent.addListener() ->
          print(DamageEvent.getSource().getName() + " dealt " + DamageEvent.getAmount().toString() + " damage to " + DamageEvent.getTarget().getName())
          print("id " + DamageEvent.getId().toString() + "; element " + DamageEvent.getElement().getName() + ".")
          print("OrigAm " + DamageEvent.getOriginalAmount().toString() + "; UnredAm" + DamageEvent.getUnreducedAmount().toString())

init
  nullTimer() ->
    initFrames()
    startEntityLoop()
    initPlayers()
    initPlayersGold()
    initVision()
    initPickTimer()
    initHeroPickMenu()
    initItemShop()
    initCheatCodes()
    doAfter(5) ->
      BlzChangeMinimapTerrainTex("originalMap.blp")