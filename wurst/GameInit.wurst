package GameInit
import GameProperties
import EntityManagement
import WaveController
import PlayerData
import ItemShopMenu
import HeroPickMenu

constant arena1Rect = Rect(-2272, -1760, 2560, 1760)

function initFrames()
  let loadingResult = BlzLoadTOCFile("MyFiles.toc")
  if loadingResult == false
    print("SOMETHING WENT WRONG WITH MyFiles.toc")

function initPlayersGold()
  pData.forEach() (player p, PlayerData playerData) ->
    p.setGold(150)

function initVision()
  SetPlayerHandicap(ENEMY_PLAYER, pData.size() / 8.)
  createVision(ENEMY_PLAYER, playableMapRect, false).start()

  pData.forEach() (player p, PlayerData playerData) ->
    createVision(p, arena1Rect, false).start()

function initPickTimer()
  let heroPickTimer = getTimer()
  let heroPickTimerDialog = heroPickTimer.createTimerDialog()
  heroPickTimerDialog
    ..setTitle("Hero pick phase ends in:")
    ..display(true)

  heroPickTimer.doAfter(HERO_PICK_TIME) ->
    heroPickTimerDialog.destr()
    waveController.startWaveSpawn()
        
init
  nullTimer() -> 
    initFrames()
    initPlayers()
    initPlayersGold()
    initVision()
    startEntityLoop()
    initPickTimer()
    initHeroPickMenu()
    initItemShopMenu()