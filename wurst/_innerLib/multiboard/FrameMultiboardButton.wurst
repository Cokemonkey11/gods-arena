package FrameMultiboardButton
import FrameMultiboard
import HashMap
import ClosureFrames

constant hotkeyMap = new HashMap<trigger, FrameMultiboard>

function framehandle.removeFocus()
  this.disable()
  this.enable()

public class ToggleMultiboardButton
  static var buttonFrameContextCounter = 0

  private constant buttonFrame = createFrame("GeneralButton", GAME_UI, 0, 0)

  FrameMultiboard mboard
  
  construct(FrameMultiboard mboard, string title)
    this.mboard = mboard
    buttonFrame
    ..setSize(0.12, 0.032)
    ..setAbsPoint(FRAMEPOINT_BOTTOM, vec2(0.46, 0.138))
    ..setText(title)
    ..setScale(0.9)
    ..hide()
    ..onClick() ->
      let p = GetTriggerPlayer()
      this.buttonFrame.removeFocus()

      mboard.toggleView(p)

      buttonFrameContextCounter++

  function setSize(real width, real height)
    buttonFrame.setSize(width, height)

  function setPoint(framepointtype framePoint, vec2 size)
    buttonFrame.setAbsPoint(framePoint, size)

  function show(player p)
    this.buttonFrame.show(p)

  function addHotkeyForPlayer(player p, oskeytype key)
    let trig = CreateTrigger()
    ..addAction() ->
      let clicker = GetTriggerPlayer()
      let mboard = hotkeyMap.get(GetTriggeringTrigger())

      mboard.toggleView(clicker)

    BlzTriggerRegisterPlayerKeyEvent(trig, p, key, 0, false)
    hotkeyMap.put(trig, mboard)