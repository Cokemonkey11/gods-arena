package HolySmite
import Icons
import Abilities
import ClosureForGroups
import AbilityTooltipGenerator
import InstantDummyCaster
import StunAbilityPreset
import TheEmpyreanBuff
import HeroSpellConstants
import PassiveAbilityPreset

public constant HOLY_SMITE_ID = compiletime(ABIL_ID_GEN.next())
public constant HOLY_SMITE_STUN_ID = compiletime(ABIL_ID_GEN.next())

RealLevelClosure CHANCE = lvl -> 19.
RealLevelClosure DAMAGE = lvl -> 55. + 35 * (lvl-1)
RealLevelClosure RADIUS = lvl -> 225
RealLevelClosure STUN_DURATION = lvl -> 0.3

init 
  DamageEvent.addListener() -> 
    let sunit = DamageEvent.getSource()

    if DamageEvent.getType() == DamageType.ATTACK and sunit.hasAbility(HOLY_SMITE_ID)
      let lvl = sunit.getAbilityLevel(HOLY_SMITE_ID)

      let bEmpyreanBuff = sunit.getBuff(TheEmpyreanBuff.typeId)
      let hasEmpyreanBuff = bEmpyreanBuff != null

      var spellChance = CHANCE.run(lvl)

      if hasEmpyreanBuff
        let empyreanBuff = bEmpyreanBuff castTo TheEmpyreanBuff
        spellChance = empyreanBuff.holySmiteProcChance

      if spellChance >= GetRandomInt(1, 100)
        let tunit = DamageEvent.getTarget()
        let spellDamage = DAMAGE.run(lvl)
        let spellRadius = RADIUS.run(lvl)

        flashEffect(Abilities.thunderclapCaster, tunit.getPos())

        forUnitsInRange(tunit.getPos(), spellRadius) target ->
          if target.isEnemyOf(sunit) and target.isAlive()
            DamageEvent.setNextDamageFromCode()
            sunit.damageTarget(target, spellDamage)
            InstantDummyCaster.castTarget(DUMMY_PLAYER, HOLY_SMITE_STUN_ID, lvl, "thunderbolt", target) 

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator(Targettype.PASSIVE, "Gives chance on attack to land a heavy strike, which deals fixed damage and stuns enemies in small area.")
  new PassiveAbilityPreset(HOLY_SMITE_ID, NORMAL_SPELL_LEVELS, tooltip)
  ..setLevelSkipRequirement(0)
  ..presetIcon(Icons.pASBTNFeedBack)
  ..presetButtonPosNormal(2, 2)
  ..presetButtonPosResearch(2, 0)
  ..presetHotkey("E")

  ..tooltipStartListen()
  ..setName("Holy Smite")
  ..addTooltipProperty("Chance", CHANCE)
  ..addTooltipProperty("Damage", DAMAGE)
  ..presetAreaofEffect(RADIUS)
  ..addTooltipProperty("Stun Duration", STUN_DURATION)
  ..tooltipStopListen()

  new StunAbilityPreset(HOLY_SMITE_STUN_ID, 5)
  ..presetDurationHero(STUN_DURATION)
  ..presetDurationNormal(STUN_DURATION)