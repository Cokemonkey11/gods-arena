package Bloodlust
import AbilityTooltipGenerator
import GameConstants
import Icons
import TooltipFactory

public constant BLOODLUST_ID = compiletime(ABIL_ID_GEN.next())

constant RealLevelClosure AS_BONUS = lvl -> 0.2 + 0.1 * (lvl-1)
constant RealLevelClosure MS_BONUS = lvl -> 0.1 + 0.05 * (lvl-1)
constant RealLevelClosure DURATION = lvl -> 12.

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator(Targettype.UNIT, "Increases a friendly unit's attack rate and movement speed.")
  new AbilityDefinitionBloodlustCreep(BLOODLUST_ID)
  ..registerTooltipGenerator(tooltip)
  ..setRequiredLevel(1)
  ..presetIcon(Icons.bTNBloodLust)
  ..presetButtonPosNormal(1, 2)
  ..presetButtonPosTurnOff(1, 2)
  ..presetButtonPosResearch(1, 0)
  ..presetHotkey("W")
  
  ..tooltipStartListen()
  ..setLevels(NORMAL_SPELL_LEVELS)
  ..setName("Bloodlust")
  ..addTooltipProperty("Attack Speed Bonus", lvl -> (AS_BONUS.run(lvl) * 100).percRound())
  ..addTooltipProperty("Movespeed Bonus", lvl -> (MS_BONUS.run(lvl) * 100).percRound())
  ..addTooltipProperty("Duration", DURATION)
  ..presetCastRange(lvl -> 600)
  ..presetManaCost(lvl -> 75 + 25 * lvl)
  ..presetCooldown(lvl -> 18. - lvl * 2)
  ..tooltipStopListen()
  
  ..presetAttackSpeedIncrease(AS_BONUS)
  ..presetMovementSpeedIncrease(MS_BONUS)
  ..presetDurationNormal(DURATION)
  ..presetDurationHero(DURATION)
  ..presetScalingFactor(lvl -> 0)
  ..presetTargetsAllowed(lvl -> commaList(TargetsAllowed.friend, TargetsAllowed.self))