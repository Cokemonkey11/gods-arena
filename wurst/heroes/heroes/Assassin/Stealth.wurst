package Stealth
import AbilityTooltipGenerator
import Icons
import GameConstants

public constant STEALTH_ID = compiletime(ABIL_ID_GEN.next())

RealLevelClosure BACKSTAB_DAMAGE = lvl -> 50. + 50. * (lvl-1)
RealLevelClosure BONUS_MOVESPEED = lvl -> 2.5 + 2.5 * (lvl-1)
RealLevelClosure DURATION = lvl -> 3.

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator("Hero becomes invisible for a short time and increases his movespeed. First attack while in stealth deals bonus damage.")
  new AbilityDefinitionBladeMasterWindWalk(STEALTH_ID)
  ..registerTooltipGenerator(tooltip)
  ..presetIcon(Icons.bTNShadowMeld)
  ..presetButtonPosNormal(1, 2)
  ..presetButtonPosResearch(1, 0)
  
  ..tooltipStartListen()
  ..setLevels(NORMAL_SPELL_LEVELS)
  ..presetHotkey("W")
  ..setName("Stealth")
  ..presetBackstabDamage1(BACKSTAB_DAMAGE)
  ..presetMovementSpeedIncrease(BONUS_MOVESPEED) 
  ..addTooltipProperty("Duration", DURATION)
  ..presetManaCost(lvl -> 50)
  ..presetCooldown(lvl -> 7)
  ..tooltipStopListen()

  ..presetDurationHero(DURATION)
  ..presetDurationNormal(DURATION)