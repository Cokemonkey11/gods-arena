package CoupDeGrace
import AbilityTooltipGenerator
import GameConstants
import PassiveAbilityPreset
import DamageSystem
import MapIcons
import ClosureEvents
import TooltipFactory

public constant COUP_DE_GRACE_ID = compiletime(ABIL_ID_GEN.next())

constant RealLevelClosure CRITICAL_CHANCE = lvl -> 10. + 5. * (lvl-1)
constant RealLevelClosure CRITICAL_MULT = lvl -> 1.8 + 0.4 * (lvl-1)
constant RealLevelClosure BACKSTAB_MULT = lvl -> 2.

constant CONE_ANGLE = 140.

init
  DamageEvent.addUnreducedListener(DamageEvents.START castTo int) -> 
    let sunit = DamageEvent.getSource()

    if sunit.hasAbility(COUP_DE_GRACE_ID) and DamageEvent.getType() == DamageType.ATTACK 
      let lvl = sunit.getAbilityLevel(COUP_DE_GRACE_ID)
      let tunit = DamageEvent.getTarget()
      var chance = CRITICAL_CHANCE.run(lvl)

      let facing = tunit.getFacingAngle()
      let back = facing - (180).fromDeg()
      let targetAngle = tunit.getPos().angleTo(sunit.getPos())
      let delta = Atan2((targetAngle - back).sin(), (targetAngle - back).cos()).abs()
      if (delta < CONE_ANGLE / 2.)
        chance *= 2.

      sunit
      ..addOnetimeStat(CustomUnitStats.ATTACK_CRIT_CHANCE, chance/100)
      
  EventListener.add(EVENT_PLAYER_HERO_SKILL) ->
    let u = EventData.getTriggerUnit()

    if EventData.getLearnedSkill() == COUP_DE_GRACE_ID
      let lvl = u.getAbilityLevel(COUP_DE_GRACE_ID)
      if lvl != 1
        let multiplayerOld = CRITICAL_MULT.run(lvl-1)
        u.addStat(CustomUnitStats.ATTACK_CRIT_POWER, -multiplayerOld)
      
      let multiplayer = CRITICAL_MULT.run(lvl)
      u.addStat(CustomUnitStats.ATTACK_CRIT_POWER, multiplayer)

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator(Targettype.PASSIVE, "Hero empowers his ability to deal critical strikes greatly. Bonus chance to deal critical strike is doubled if target is attacked from behind.")
  new PassiveAbilityPreset(COUP_DE_GRACE_ID, ULTIMATE_SPELL_LEVELS, tooltip)
  ..setRequiredLevel(ULTIMATE_LEVEL_REQ)
  ..setLevelSkipRequirement(ULTIMATE_LEVEL_SKIP_REQ)
  ..presetButtonPosNormal(3, 2)
  ..presetButtonPosResearch(3, 0)
  ..presetHotkey("R")
  ..presetIcon(MapIcons.pASBTNBackstab)

  ..tooltipStartListen()
  ..setName("Coup De Grace")
  ..addTooltipProperty("Critical Strike Chance", lvl -> CRITICAL_CHANCE.run(lvl).percRound())
  ..addTooltipProperty("Damage Multiplayer", lvl -> (CRITICAL_MULT.run(lvl) * 100).percRound())
  ..addTooltipProperty("Backstab Chance Multiplayer", BACKSTAB_MULT)
  ..tooltipStopListen()