package CoupDeGrace
import AbilityTooltipGenerator
import Icons
import DamageEvent
import StandardTextTags
import HeroSpellConstants
import PassiveAbilityPreset

public constant COUP_DE_GRACE_ID = compiletime(ABIL_ID_GEN.next())

constant RealLevelClosure CRITICAL_CHANCE = lvl -> 15. + 5. * (lvl-1)
constant RealLevelClosure CRITICAL_MULT = lvl -> 2.5 + 0.5 * (lvl-1)
constant RealLevelClosure BACKSTAB_MULT = lvl -> 2.

constant CONE_ANGLE = 140.

init
  DamageEvent.addUnreducedListener() -> 
    let sunit = DamageEvent.getSource()

    if sunit.hasAbility(COUP_DE_GRACE_ID) and DamageEvent.getType() == DamageType.ATTACK 
      let lvl = sunit.getAbilityLevel(COUP_DE_GRACE_ID)
      let tunit = DamageEvent.getTarget()
      var chance = CRITICAL_CHANCE.run(lvl)


      let facing = tunit.getFacingAngle()
      let back = facing - (180).fromDeg()
      let targetAngle = tunit.getPos().angleTo(sunit.getPos())
      let delta = Atan2((targetAngle - back).sin(), (targetAngle - back).cos()).abs()
      if (delta < CONE_ANGLE / 2.)
        chance *= 2.
      

      if chance >= GetRandomInt(1, 100)
        let multiplayer = CRITICAL_MULT.run(lvl)
        let currentDamage = DamageEvent.getUnreducedAmount()
        let damageToAdd = currentDamage * (multiplayer-1)
        let resultDamage = currentDamage * multiplayer
      
        DamageEvent.addAmount(damageToAdd)
        createCriticalStrikeTextTag(sunit, resultDamage.toInt())
        flashEffect("Coup de Grace.mdx", tunit.getPos(), 1, sunit.getFacingAngle())

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator(Targettype.PASSIVE, "Hero gains chance to deal critical strike. Chance to deal critical strike is doubled if target is attacked from behind.")
  new PassiveAbilityPreset(COUP_DE_GRACE_ID, ULTIMATE_SPELL_LEVELS, tooltip)
  ..setRequiredLevel(ULTIMATE_LEVEL_REQ)
  ..setLevelSkipRequirement(ULTIMATE_LEVEL_SKIP_REQ)
  ..presetButtonPosNormal(3, 2)
  ..presetButtonPosResearch(3, 0)
  ..presetHotkey("R")
  ..presetIcon(Icons.bTNSacrifice)

  ..tooltipStartListen()
  ..setName("Coup De Grace")
  ..addTooltipProperty("Critical Strike Chance", CRITICAL_CHANCE)
  ..addTooltipProperty("Damage Multiplayer", CRITICAL_MULT)
  ..addTooltipProperty("Backstab Chance Multiplayer", BACKSTAB_MULT)
  ..tooltipStopListen()