package Bladestorm
import AbilityTooltipGenerator
import HeroSpellConstants

public constant BLADESTORM_ID = compiletime(ABIL_ID_GEN.next())

RealLevelClosure DURATION = lvl -> 6. + lvl

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator(Targettype.NONE, "Causes a bladestorm of destructive force around the hero, rendering him immune to magic and dealing damage per second to nearby enemy units.")
  new AbilityDefinitionBladeMasterBladestorm(BLADESTORM_ID)
  ..registerTooltipGenerator(tooltip)
  ..setLevelSkipRequirement(ULTIMATE_LEVEL_SKIP_REQ)
  ..setRequiredLevel(ULTIMATE_LEVEL_REQ)
  ..presetButtonPosNormal(3, 2)
  ..presetButtonPosResearch(3, 0)
  ..presetHotkey("R")

  ..tooltipStartListen()
  ..setLevels(ULTIMATE_SPELL_LEVELS)
  ..setName("Bladestorm")
  ..presetDamagePerSecond(lvl -> 60. + 80. * lvl)
  ..presetAreaofEffect(lvl -> 220)
  ..addTooltipProperty("Duration", DURATION)
  ..presetManaCost(lvl -> 90 + 30 * lvl)
  ..presetCooldown(lvl -> 120)
  ..tooltipStopListen()

  ..presetDurationNormal(DURATION)
  ..presetDurationHero(DURATION)

