package PowerStrike
import AbilityTooltipGenerator
import HeroSpellConstants
import Icons

public constant POWER_STRIKE_ID = compiletime(ABIL_ID_GEN.next())

RealLevelClosure DAMAGE_DEALT = lvl -> 20. + 40. * lvl
RealLevelClosure CHANCE_TO_STOMP = lvl -> 15. + 5. * lvl

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator(Targettype.PASSIVE, "Gives a chance that an attack will deal bonus damage to nearby units.")
  new AbilityDefinitionPulverizecreep(POWER_STRIKE_ID)
  ..registerTooltipGenerator(tooltip)
  ..presetIcon(Icons.bTNSmash)
  ..presetButtonPosNormal(1, 2)
  ..presetButtonPosResearch(1, 0)
  ..presetHotkey("")

  ..tooltipStartListen()
  ..setLevels(NORMAL_SPELL_LEVELS)
  ..setName("Power Strike")
  ..presetDamageDealt(DAMAGE_DEALT)
  ..addTooltipProperty("Chance to Stomp", lvl -> (CHANCE_TO_STOMP.run(lvl)).toString()+"%")
  ..tooltipStopListen()
  
  ..presetChancetoStomp(CHANCE_TO_STOMP)
  ..presetFullDamageRadius(lvl -> 250)
  ..presetHalfDamageRadius(lvl -> 250)
