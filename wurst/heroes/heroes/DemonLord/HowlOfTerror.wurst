package HowlOfTerror
import Abilities
import AbilityTooltipGenerator
import BuffIds
import DamageEvent
import DemonLordIds
import GameConstants
import Icons
import StandardTextTags
import TooltipFactory

RealLevelClosure DAMAGE_DECREASE = lvl -> 0.25 + 0.1 * (lvl-1)
RealLevelClosure DURATION = lvl -> 10
IntLevelClosure STR_BONUS = _ -> 1
RealLevelClosure HP_RECOVERY = lvl -> 100.0 + 50 * (lvl-1)

init
  DamageEvent.addListener(5) ->
    let target = DamageEvent.getTarget()
    if DamageEvent.getAmount() > target.getHP()
      let killer = DamageEvent.getSource()
      if (
        killer.getTypeId() == DEMON_LORD_ID
        and target.hasAbility(BuffIds.howlofTerror)
        and not target.isType(UNIT_TYPE_SUMMONED)
      )
        let level = killer.getAbilityLevel(HOWL_OF_TERROR_ID)
        let str_bonus = STR_BONUS.run(level)
        let killer_pos = killer.getPos()
        killer.addStr(str_bonus)
        killer.addHP(HP_RECOVERY.run(level))
        standardTextTag(killer_pos, "+" + str_bonus.toString())
          ..setColor(255, 0, 0, 255)
          ..setVisibility(localPlayer == killer.getOwner())
        flashEffect(Abilities.deathCoilMissile, killer_pos)

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator(
    Targettype.NONE,
    "The Demon Lord lets loose a terrifying howl that causes nearby enemy units to shiver in fear. "
    + "Feared units have reduced attack damage. "
    + "If the Demon Lord kills a feared unit, it permanently gains strength and recovers HP."
  )
  new AbilityDefinitionPitLordHowlofTerror(HOWL_OF_TERROR_ID)
  ..registerTooltipGenerator(tooltip)
  ..presetIcon(Icons.bTNHowlOfTerror)
  ..presetButtonPosNormal(1, 2)
  ..presetButtonPosResearch(1, 0)
  ..presetHotkey("W")

  ..tooltipStartListen()
  ..setLevels(NORMAL_SPELL_LEVELS)
  ..setName("Howl of Terror")
  ..addTooltipProperty("Damage Decrease", lvl -> (DAMAGE_DECREASE.run(lvl) * 100).percRound())
  ..addTooltipProperty("Bonus Strength", STR_BONUS)
  ..addTooltipProperty("HP Recovered", HP_RECOVERY)
  ..presetAreaofEffect(lvl -> 400)
  ..addTooltipProperty("Duration", DURATION)
  ..presetManaCost(lvl -> 60)
  ..presetCooldown(lvl -> 25)
  ..tooltipStopListen()

  ..presetDamageIncrease(DAMAGE_DECREASE)
  ..presetDurationNormal(DURATION)
  ..presetDurationHero(DURATION)
