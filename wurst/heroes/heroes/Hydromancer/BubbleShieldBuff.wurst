package BubbleShieldBuff
import public PresetBuffs
import Assets
import SoundUtils
import WaterElemental
import BonusHandler

constant buffTuple = compiletime(createDummyBuffObject("Bubble Shield", "Increases unit's hp regeneration and can block small amount of damage",  Icons.bTNNeutralManaShield, Abilities.manaShieldCaster))

public class BubbleShieldBuff extends ShieldBuff
  real healAmount
  var bonusDamage = 0.

  private constant ON_APPLY_SOUND = new SoundDefinition(Sounds.manaShieldCaster1)
  
  construct(real dur, real shieldDurabiblity, real healAmount)
    super(dur, buffTuple, shieldDurabiblity)
    this.healAmount = healAmount

  override function onApply()
    if WATER_ELEM_LIST.has(target.getTypeId())
      bonusDamage = target.getBaseDamage(0).toReal()
      target.addBonus(Bonus.DAMAGE, bonusDamage)
    ON_APPLY_SOUND.play()

  override function onEnd()
    if bonusDamage != 0
      target.addBonus(Bonus.DAMAGE, -bonusDamage)

  override function update()
    super.update()
    target.addHP(healAmount)
    