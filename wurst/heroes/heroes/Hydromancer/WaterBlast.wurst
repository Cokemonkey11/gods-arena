package WaterBlast
import Icons
import AbilityTooltipGenerator
import HeroSpellConstants
import ClosureEvents
import ClosureForGroups
import SoundUtils
import WaterBlastBuff

public constant WATER_BLAST_ID = compiletime(ABIL_ID_GEN.next())

constant SOUND = new SoundDefinition(Sounds.crushingWaveTarget1)

constant RealLevelClosure DAMAGE = lvl -> 70. + 70. * (lvl-1)
constant RealLevelClosure SLOW = lvl -> 30. + 5. * (lvl-1)
constant RealLevelClosure SLOW_DURATION = lvl -> 3 + 0.5 * lvl
constant RealLevelClosure AOE = lvl -> 225.

init 
  EventListener.onPointCast(WATER_BLAST_ID) (unit caster, vec2 target) ->
    let lvl = caster.getAbilityLevel(WATER_BLAST_ID)
    let spellDamage = DAMAGE.run(lvl)
    let spellAOE = AOE.run(lvl)
    let buffSlow = SLOW.run(lvl)
    let buffDuration = SLOW_DURATION.run(lvl)

    SOUND.play(1, 40)
    forUnitsInRange(target, spellAOE) (unit u) ->
      if u.isAlive() and not u.isAllyOf(caster.getOwner())
        new WaterBlastBuff(buffDuration, buffSlow).apply(u)
        caster.damageTarget(u, spellDamage)

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator("Deals damage to enemies in small radius and slows them.")
  new ChannelAbilityPreset(WATER_BLAST_ID, NORMAL_SPELL_LEVELS, true, tooltip)
  ..presetIcon(Icons.bTNOrbOfFrost)
  ..presetButtonPosNormal(0, 2)
  ..presetButtonPosResearch(0, 0)
  ..presetHotkey("Q")
  ..setArtEffect("WaterBlast.mdx")
  ..setEffectSound(Sounds.crushingWaveTarget1)
  ..presetOption(Option.TARGETIMAGE, true)

  ..tooltipStartListen()
  ..presetTargetTypes(Targettype.POINT)
  ..setName("Water Blast")
  ..addTooltipProperty("Damage", DAMAGE)
  ..addTooltipProperty("Slow", lvl -> SLOW.run(lvl).toString())
  ..addTooltipProperty("Duration", SLOW_DURATION)
  ..presetAreaofEffect(AOE)
  ..presetCastRange(lvl -> 600)
  ..presetManaCost(lvl -> 80 + 15 * lvl)
  ..presetCooldown(lvl -> 12.)
  ..tooltipStopListen()