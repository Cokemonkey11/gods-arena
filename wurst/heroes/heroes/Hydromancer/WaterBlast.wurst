package WaterBlast
import Icons
import AbilityTooltipGenerator
import HeroSpellConstants
import ClosureEvents
import ClosureForGroups
import SoundUtils

public constant WATER_BLAST_ID = compiletime(ABIL_ID_GEN.next())

constant SOUND = new SoundDefinition(Sounds.crushingWaveTarget1)

constant RealLevelClosure DAMAGE = lvl -> 125. + 55. * (lvl-1)
constant RealLevelClosure AOE = lvl -> 200.

init 
  EventListener.onPointCast(WATER_BLAST_ID) (unit caster, vec2 target) ->
    let lvl = caster.getAbilityLevel(WATER_BLAST_ID)
    let spellDamage = DAMAGE.run(lvl)
    let spellAOE = AOE.run(lvl)

    SOUND.play()
    forUnitsInRange(target, spellAOE) (unit u) ->
      if u.isAlive() and not u.isAllyOf(caster.getOwner())
        caster.damageTarget(u, spellDamage)

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator("Deals damage to enemies in small radius.")
  new ChannelAbilityPreset(WATER_BLAST_ID, NORMAL_SPELL_LEVELS, true, tooltip)
  ..presetButtonPosNormal(0, 2)
  ..presetButtonPosResearch(0, 0)
  ..presetIcon(Icons.bTNOrbOfFrost)
  ..setArtEffect("WaterBlast.mdx")
  ..setEffectSound(Sounds.crushingWaveTarget1)

  ..tooltipStartListen()
  ..presetTargetTypes(Targettype.POINT)
  ..presetHotkey("Q")
  ..setName("Water Blast")
  ..addTooltipProperty("Damage", DAMAGE)
  ..presetAreaofEffect(AOE)
  ..presetManaCost(lvl -> 75 + 10 * lvl)
  ..presetCooldown(lvl -> 6.)
  ..tooltipStopListen()