package BubbleShield
import AbilityTooltipGenerator
import HeroSpellConstants
import Icons
import ClosureEvents
import BubbleShieldBuff

public constant BUBBLE_SHIELD_ID = compiletime(ABIL_ID_GEN.next())

constant RealLevelClosure HP_REGEN = lvl -> 10. + 8. * (lvl-1)
constant RealLevelClosure SHIELD_DURABILITY = lvl -> 50. + 100. * lvl
constant RealLevelClosure DURATION = lvl -> 8

init
  EventListener.onTargetCast(BUBBLE_SHIELD_ID) (unit caster, unit target) ->
    let lvl = caster.getAbilityLevel(BUBBLE_SHIELD_ID)
    let spellHPRegen = HP_REGEN.run(lvl)
    let shieldDurability = SHIELD_DURABILITY.run(lvl)
    let spellDuration = DURATION.run(lvl)

    new BubbleShieldBuff(spellDuration, shieldDurability, spellHPRegen).apply(target)

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator("Applies bubble shield to the target, which increases its hp regeneration. Bubble shield can block small amount of damage and brokes if block limit was exceeded.")
  new ChannelAbilityPreset(BUBBLE_SHIELD_ID, NORMAL_SPELL_LEVELS, true, tooltip)
  ..presetIcon(Icons.bTNNeutralManaShield)
  ..presetButtonPosNormal(2, 2)
  ..presetButtonPosResearch(2, 0)
  ..presetHotkey("E")
  ..presetDurationHero(DURATION)
  ..presetDurationNormal(DURATION)
  ..presetTargetsAllowed(lvl -> commaList(TargetsAllowed.allies, TargetsAllowed.self, TargetsAllowed.friend))
  
  ..tooltipStartListen()
  ..presetTargetTypes(Targettype.UNIT)
  ..setName("Bubble Shield")
  ..addTooltipProperty("Bonus HP Regeneration", HP_REGEN)
  ..addTooltipProperty("Shield Duratibility", SHIELD_DURABILITY)
  ..addTooltipProperty("Duration", DURATION)
  ..presetCastRange(lvl -> 600)
  ..presetManaCost(lvl -> 110 + 30 * lvl)
  ..presetCooldown(lvl -> 14. - lvl)
  ..tooltipStopListen()